Title,string="OBLA Gun";

Option, ECHO=FALSE;
Option, PSDUMPFREQ=10;
Option, VERSION=10600;

REAL Edes=1.0E-9;
REAL gamma=(Edes+EMASS)/EMASS;
REAL beta=sqrt(1-(1/gamma^2));
REAL gambet=gamma*beta;
REAL P0 = gamma*beta*EMASS;
REAL brho = (EMASS*1.0e9*gambet) / CLIGHT;

value,{gamma,brho,Edes,beta,gambet};

// L:        physical element length (real)
// KS:       field scaling factor (real)
// FMAPFN:   field file name (string)
// ELEMEDGE: physical start of the element on the floor (real)
SP1: Solenoid, L=1.20, ELEMEDGE=-0.5335, FMAPFN="1T2.T7",
     KS=0.00011;
SP2: Solenoid, L=1.20, ELEMEDGE=-0.399, FMAPFN="1T3.T7", KS=0.0;
SP3: Solenoid, L=1.20, ELEMEDGE=-0.269, FMAPFN="1T3.T7", KS=0.0;

gun: RFCavity, L=0.011, VOLT=-102.28, FMAPFN="1T1.T7",
     ELEMEDGE=0.00, TYPE="STANDING", FREQ=1.0e-6;

l1:   Line = (gun,sp1,sp2,sp3);

REAL rf=1498.956;
REAL v0=beta*CLIGHT;
REAL lz  = 6.5E-12*v0;
value,{v0,lz};

Dist1:DISTRIBUTION, TYPE=gungauss,
      sigmax=  0.00030, sigmapx=0.0, corrx=0.0,
      sigmay=  0.00030, sigmapy=0.0, corry=0.0,
      sigmat=  lz, sigmapt=1.0, corrt=0.0 ,
      NBIN=39;

Fs1:FIELDSOLVER, FSTYPE=FFT, MX=32, MY=32, MT=32,
                 PARFFTX=true, PARFFTY=true, PARFFTT=false,
                 BCFFTX=open, BCFFTY=open, BCFFTT=open,
                 BBOXINCR=1, GREENSF=STANDARD;

beam1: BEAM, PARTICLE=ELECTRON, pc=P0, NPART=50000,
       BCURRENT=0.008993736, BFREQ=rf, CHARGE=-1;

Select, Line=l1;
track,line=l1, beam=beam1, MAXSTEPS=2000, DT=1.0e-13;
 run, method = "PARALLEL-T", beam=beam1, fieldsolver=Fs1,
      distribution=Dist1;
endtrack;

Stop;