Option, ECHO=FALSE;
Option, TFS=FALSE;
Option, PSDUMPFREQ=10;
Option, VERSION=10600;

Title,string="OPAL Diagnostics test";

REAL Edes=0.0307; //GeV
REAL gamma=(Edes+EMASS)/EMASS;
REAL beta=sqrt(1-(1/gamma^2));
REAL gambet=gamma*beta;
REAL P0 = gamma*beta*EMASS;
REAL brho = (EMASS*1.0e9*gambet) / CLIGHT;
value,{gamma,brho,Edes,beta,gambet};

FINLB02_MSLAC40: Solenoid, L=0.001, KS=0.05, 
                 FMAPFN="FINLB02-MSLAC.T7", ELEMEDGE=4.554;

FIND1_MQ10: Quadrupole, L=0.1, K1=2.788, ELEMEDGE=5.874;
FIND1_MQ20: Quadrupole, L=0.1, K1=-3.517, ELEMEDGE=6.074;


SCREEN: Monitor, L=0.01, ELEMEDGE=7.3867, OUTFN="Screen.h5";

FIND1: Line = (FINLB02_MSLAC40, FIND1_MQ10, FIND1_MQ20, SCREEN);

Dist1:DISTRIBUTION, TYPE=gauss,
      sigmax=  1.0e-03, sigmapx=1.0e-4, corrx=0.5,
      sigmay=  2.0e-03, sigmapy=1.0e-4, corry=-0.5,
      sigmat=  3.0e-03, sigmapt=1.0e-4, corrt=0.0;

Fs2:FIELDSOLVER, FSTYPE=FFT, MX=32, MY=32, MT=64, 
    PARFFTX=false, PARFFTY=false, PARFFTT=true,
    BCFFTX=open, BCFFTY=open, BCFFTT=open,
    BBOXINCR=1.0, GREENSF=INTEGRATED;

beam1: BEAM, PARTICLE=ELECTRON, pc=P0, NPART=1e5, 
       BFREQ=1498.953425154, BCURRENT=0.299598, CHARGE=-1;

Select, Line=FIND1;

track,line=FIND1, beam=beam1, MAXSTEPS=10000, DT=1.0e-12;
 run, method = "PARALLEL-T", beam=beam1, fieldsolver=Fs2, 
      distribution=Dist1;
endtrack;
Stop;
