OPTION, VERSION = 20200;

OPTION, ECHO= FALSE;
OPTION, PSDUMPFREQ= 10;
OPTION, PSDUMPFRAME= REFERENCE;

//////////////////////////////////////////////////////////////////////////////
// input file for single particle tracking                                  //
// the initial conditions are obtained from FIXPO GORBIT mode.              //
//////////////////////////////////////////////////////////////////////////////

TITLE, STRING= "Ring AEO";

REAL nstep = 1800;
REAL nturn = 190;

REAL Edes = 0.072;
REAL pr0  = -0.0154;
REAL r0   = 2045;

REAL phi01 = 192.0+12;
REAL phi02 = phi01+180.0;
REAL phi04 = phi01;
REAL phi05 = phi01+180.0;
REAL phi03 = 111.40+36+2;

REAL volt1st = 0.847;
REAL ptg     = 1.465;
REAL volt3rd = volt1st*4.0*0.112*ptg;

REAL r1   = 1003.38;
REAL r2   = 6203.38;
REAL rft1 = 1845.45;
REAL rft2 = 4477.24;

REAL f1 =  50.65;
REAL f3 = 3.0*f1;

REAL gamma=(Edes+PMASS)/PMASS;
REAL beta=sqrt(1-(1/gamma^2));
REAL gambet=gamma*beta;
REAL P0 = gamma*beta*PMASS;

IF (OPALVERSION >= 20100){
    tc1: TRIMCOIL, TYPE="PSI-BFIELD-MIRRORED", RMIN = 4350.0, RMAX = 4470.0, BMAX=1.4e-3, SLPTC=0.6;
} ELSE {
    tc1: TRIMCOIL, TYPE="PSI-RING-OLD",        RMIN = 4350.0, RMAX = 4470.0, BMAX=1.4e-3, SLPTC=0.6;
}

RingCycl: CYCLOTRON, TYPE=RING, CYHARMON=6, PHIINIT=110.0, PRINIT=pr0, RINIT=r0 , SYMMETRY=8.0, RFFREQ=f1, FMAPFN="bfield.dat", TRIMCOIL=tc1;

Rf0: RFCAVITY, VOLT=volt1st, FMAPFN="rffield1.dat", TYPE=SINGLEGAP,
FREQ=f1, RMIN = r1, RMAX = r2, ANGLE=35.0,  PDIS = 416.0, GAPWIDTH = 300.0, PHI0=phi01;

Rf1: RFCAVITY, VOLT=volt1st, FMAPFN="rffield1.dat", TYPE=SINGLEGAP,
FREQ=f1, RMIN = r1, RMAX = r2, ANGLE=125.0, PDIS = 416.0, GAPWIDTH = 300.0, PHI0=phi02;

Rf3: RFCAVITY, VOLT=volt1st, FMAPFN="rffield1.dat", TYPE=SINGLEGAP,
FREQ=f1, RMIN = r1, RMAX = r2, ANGLE=215.0, PDIS = 416.0, GAPWIDTH = 300.0, PHI0=phi04;

Rf4: RFCAVITY, VOLT=volt1st, FMAPFN="rffield1.dat", TYPE=SINGLEGAP,
FREQ=f1, RMIN = r1, RMAX = r2, ANGLE=305.0, PDIS = 416.0, GAPWIDTH = 300.0, PHI0=phi05;

Rf2: RFCAVITY, VOLT=volt3rd, FMAPFN="rffield2.dat", TYPE=SINGLEGAP,
FREQ=f3, RMIN = rft1, RMAX = rft2, ANGLE=260.0, PDIS = 452.0, GAPWIDTH = 250.0, PHI0=phi03;

L1:   LINE = (RingCycl,Rf0,Rf1,Rf2,Rf3,Rf4);

Dist1:DISTRIBUTION, TYPE=FROMFILE, FNAME="dist2.dat";

Fs1:FIELDSOLVER, FSTYPE=NONE, MX=64, MY=64, MT=64, PARFFTX=true, PARFFTY=true, PARFFTT=false,
                 BCFFTX=open, BCFFTY=open, BCFFTT=open;

Beam1: BEAM, PARTICLE=PROTON, pc=P0, NPART=1, BCURRENT=1.0E-6, BFREQ=f1;

SELECT, LINE=L1;

TRACK, LINE= L1, BEAM= Beam1, MAXSTEPS= nstep*nturn, STEPSPERTURN=nstep;
RUN, METHOD = "CYCLOTRON-T", BEAM= Beam1, FIELDSOLVER= Fs1, DISTRIBUTION= Dist1;
ENDTRACK;
STOP;
