:stem: latexmath
:sectnums:
:toc: macro
:toclevels: 3

link:home[Back to Main Page]

toc::[]

[[chp:sample]]
Sampler 
--------
This is a modification of the https://gitlab.psi.ch/OPAL/src/wikis/Manual/optimiser[optimiser]. Instead of performing a multi-objective optimisation, it creates samples of the design variables and runs those simulations. This feature can be used for example as training / validation of neural networks or uncertainty quantification (UQ). 

**Be aware of the fact, this is an experimental feature in V2.0.0**

[[sample-commands]]
OPAL Commands
~~~~~~~~~~~~~
It uses almost the same syntax as the optimiser.

Basic Syntax
^^^^^^^^^^^^
One needs to define the design variables and their sampling method. The syntax for
design variables is described in the section https://gitlab.psi.ch/OPAL/src/wikis/Manual/optimiser[DVAR Command].

SAMPLE Command
^^^^^^^^^^^^^^
It allows random (SEQUENCE=false) and sequence mode.

[#samplecommandattributes]
.Attributes for the command SAMPLE.
[cols="<1,<5",options="header",]
|=======================================================================
| Attribute     | Description
| SEQUENCE      | Random or sequence mode (default)
| INPUT         | Path to input file.
| OUTPUT        | Name used in output file generation.
| OUTDIR        | Name of directory used to run and store generation output files.
| DVARS         | List of design variables to be used.
| SAMPLINGS     | List of sample methods to be used.
| NUM_MASTERS   | Number of master nodes (currently only 1 master supported).
| NUM_COWORKERS | Number processors per worker.
| TEMPLATEDIR   | Directory where templates are stored.
| FIELDMAPDIR   | Directory where field maps are stored.
|=======================================================================

SAMPLING Command
^^^^^^^^^^^^^^^^
[#samplingcommandattributes]
.Attributes for the command SAMPLING.
[cols="<1,<5",options="header",]
|=======================================================================
| Attribute | Description
| VARIABLE  | Name of the design variable.
| TYPE      | Sampling method (see next section).
| SEED      | Seed for random sampling (default: 42).
| FNAME     | File to read the samples from.
| N         | Number of samples per this design variable. In case of random mode, the
minimum value over all design variables is used.
|=======================================================================

Available Sampling Methods
^^^^^^^^^^^^^^^^^^^^^^^^^^
[#samplemethods]
.Available sampling methods.
[cols="<1,<5",options="header",]
|=======================================================================
| Method            | Description
| FROMFILE          | The samples are provided by a file. A column represents the
                      values of a design variable. The first line is the header reading
                      the variable name. Not all variables need to be provided in the file.
                      This allows also reading variables from different files.
| UNIFORM           | In sequence mode: Generates an equidistant sequence taking the lower and upper
                      bound of the design variable command as limits.
                      In random mode: Uniform random sampling of floats. It takes the lower and upper
                      bound of the design variable command as limits.
| UNIFORM_INT       | In sequence mode: Generates an equidistant sequence of integers taking the lower
                      and upper bound of the design variable command as limits.
                      In random mode: Uniform random sampling of integers. It takes the lower and
                      upper bound of the design variable command.
| GAUSSIAN          | In sequence mode: Generates a sequence with a gaussian distribution taking
                      the difference between upper and lower bound of the design
                      variable as latexmath:[10\;\sigma].
                      In random mode: Gaussian random sampling of floats. The upper and lower bound
                      are the latexmath:[\pm 5\; \sigma] limits of the distribution.
|=======================================================================

Example Input File
^^^^^^^^^^^^^^^^^^

....
OPTION, INFO=TRUE;

// Design variables
nstep:  DVAR, VARIABLE="nstep", LOWERBOUND=10,     UPPERBOUND=40;
MX:     DVAR, VARIABLE="MX",    LOWERBOUND=16,      UPPERBOUND=32;

// Sampling methods
SM1: SAMPLING, VARIABLE="nstep", TYPE="FROMFILE",    FNAME="samples.dat";
SM2: SAMPLING, VARIABLE="MX",    TYPE="UNIFORM_INT", SEED=122, N = 6;

SAMPLE,
    SEQUENCE        = false,
    DVARS           = {nstep, MX},
    SAMPLINGS       = {SM1, SM2},
    INPUT           = "Ring.tmpl",
    OUTPUT          = "RingSample",
    OUTDIR          = "RingSample",
    TEMPLATEDIR     = "template",
    FIELDMAPDIR     = "Fieldmaps",
    NUM_MASTERS     = 1,
    NUM_COWORKERS   = 1;
QUIT;
....

The samples of ```nstep``` are provided by ```samples.dat``` that could look like this:
....
MX    nstep
16    10
17    11
18    12
19    13
20    14
21    15
22    16
23    17
24    18
25    19
....
Although the file contains samples for ```MX```, too, they are not considered. The
corresponding template file ```Ring.tmpl``` reads:
....
Option, ECHO=FALSE;
Option, PSDUMPFREQ=100000;
Option, SPTDUMPFREQ = 10;
Option, PSDUMPEACHTURN=false;
Option, REPARTFREQ=20;
Option, ECHO=FALSE;
Option, STATDUMPFREQ=1;
Option, CZERO=FALSE;
Option, MEMORYDUMP=TRUE;
Option, TELL=TRUE;
Option, VERSION=10900;

Title,string="OPAL-cycl: the first turn acceleration in PSI 590MeV Ring";

REAL Edes=.072;
REAL gamma=(Edes+PMASS)/PMASS;
REAL beta=sqrt(1-(1/gamma^2));
REAL gambet=gamma*beta;
REAL P0 = gamma*beta*PMASS;
REAL brho = (PMASS*1.0e9*gambet) / CLIGHT;

//value,{gamma,brho,Edes,beta,gambet};

REAL phi01=139.4281;
REAL phi02=phi01+180.0;
REAL phi04=phi01;
REAL phi05=phi01+180.0;
REAL phi03=phi01+10.0;

REAL volt1st=0.9;
REAL volt3rd=0.9*4.0*0.112;

REAL turns = 1;
REAL nstep=_nstep_;

REAL frequency=50.650;
REAL frequency3=3.0*frequency;

ring: Cyclotron, TYPE="RING", CYHARMON=6, PHIINIT=0.0,
	PRINIT=-0.000174, RINIT=2130.0, SYMMETRY=8.0, RFFREQ=frequency,
	FMAPFN="s03av.nar";

rf0: RFCavity, VOLT=volt1st, FMAPFN="Cav1.dat", TYPE="SINGLEGAP",
	FREQ=frequency, RMIN = 1900.0, RMAX = 4500.0, ANGLE=35.0,  PDIS = 416.0,
	GAPWIDTH = 220.0, PHI0=phi01;

rf1: RFCavity, VOLT=volt1st, FMAPFN="Cav1.dat", TYPE="SINGLEGAP",
	FREQ=frequency, RMIN = 1900.0, RMAX = 4500.0, ANGLE=125.0, PDIS = 416.0,
	GAPWIDTH = 220.0, PHI0=phi02;

rf2: RFCavity, VOLT=volt3rd, FMAPFN="Cav3.dat", TYPE="SINGLEGAP",
	FREQ=frequency3,RMIN = 1900.0, RMAX = 4500.0, ANGLE=170.0, PDIS = 452.0,
	GAPWIDTH = 250.0, PHI0=phi03;

rf3: RFCavity, VOLT=volt1st, FMAPFN="Cav1.dat", TYPE="SINGLEGAP",
	FREQ=frequency, RMIN = 1900.0, RMAX = 4500.0, ANGLE=215.0, PDIS = 416.0,
	GAPWIDTH = 220.0, PHI0=phi04;

rf4: RFCavity, VOLT=volt1st, FMAPFN="Cav1.dat", TYPE="SINGLEGAP",
	FREQ=frequency, RMIN = 1900.0, RMAX = 4500.0, ANGLE=305.0, PDIS = 416.0,
	GAPWIDTH = 220.0, PHI0=phi05;

l1:   Line = (ring,rf0,rf1,rf2,rf3,rf4);

Dist1:DISTRIBUTION, TYPE=gauss,
	sigmax = 2.0e-03,
	sigmapx = 1.0e-7,
	corrx = 0.0,
	sigmay = 2.0e-03,
	sigmapy = 1.0e-7,
	corry = 0.0,
	sigmat = 2.0e-03,
	sigmapt = 3.394e-4,
	corrt=0.0;

Fs1:FIELDSOLVER, FSTYPE=FFT, MX=_MX_, MY=16, MT=16,
	PARFFTX=true, PARFFTY=true, PARFFTT=true,
	BCFFTX=open, BCFFTY=open, BCFFTT=open, BBOXINCR=2;

beam1: BEAM, PARTICLE=PROTON, pc=P0, NPART=8192, BCURRENT=1.0E-3, CHARGE=1.0, BFREQ= frequency;

Select, Line=l1;

TRACK,LINE=l1, BEAM=beam1, MAXSTEPS=nstep*turns, STEPSPERTURN=360,TIMEINTEGRATOR="RK-4";
 run, method = "CYCLOTRON-T", beam=beam1, fieldsolver=Fs1, distribution=Dist1;
endtrack;

Stop;
....